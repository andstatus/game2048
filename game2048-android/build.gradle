buildscript {
    repositories {
		google();
		mavenCentral()
	}
    dependencies {
		classpath 'com.android.tools.build:gradle:8.1.2'
        classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.9.20' }

}

plugins {
  id 'com.github.triplet.play' version '3.8.4'
}

repositories {
    mavenLocal()
	mavenCentral()
    google()
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    namespace "org.andstatus.game2048"
	compileOptions {
		sourceCompatibility JavaVersion.VERSION_1_8
		targetCompatibility JavaVersion.VERSION_1_8
	}
    installation {
        installOptions '-r'
        timeOutInMs 30 * 1000
    }
    lint {
        disable 'GradleCompatible'
    }
    kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs += "-Xmulti-platform"
        freeCompilerArgs += "-Xopt-in=kotlin"
    }
    packagingOptions {
        jniLibs {
            excludes += ['META-INF/LGPL*']
        }
        resources {
            excludes += [
                'META-INF/DEPENDENCIES',
                'META-INF/LICENSE',
                'META-INF/LICENSE.txt',
                'META-INF/license.txt',
                'META-INF/NOTICE',
                'META-INF/NOTICE.txt',
                'META-INF/notice.txt',
                'META-INF/LGPL*',
                'META-INF/AL2.0',
                'META-INF/*.kotlin_module',
                '**/*.kotlin_metadata',
                '**/*.kotlin_builtins'
            ]
        }
    }
    compileSdkVersion 33
    defaultConfig {
        applicationId 'org.andstatus.game2048'
        minSdkVersion 24
        targetSdkVersion 33
        versionCode 41
        versionName '1.14.3'

        archivesBaseName = "game2048-android-$versionName"
    }
    buildTypes {
        debug {
            minifyEnabled false
        }
        release {
            minifyEnabled false
        }
    }
    sourceSets {
        main {
            manifest.srcFile '../src/androidMain/AndroidManifest.xml'
            java.srcDirs += [
                    "../src/commonMain/kotlin",
                    "../src/androidMain/kotlin"]
            assets.srcDirs += [
                    "../src/commonMain/resources",
                    "../build/korgeProcessedResources/metadata/main"]
            res.srcDirs  += [
                    "../src/commonMain/resources/res"]
        }
    }
}

if (project.hasProperty("andstatus.google-play-publisher")
        && new File(project.property("andstatus.google-play-publisher").toString() + ".gradle").exists()) {
    apply from: project.property("andstatus.google-play-publisher") + ".gradle";
} else {
    play {
        enabled = false
    }
}

dependencies {
    def korgeVersion = '5.0.6'

    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.9.20'
    implementation "com.soywiz.korge:korge-android:$korgeVersion"
    implementation "com.soywiz.korge:korge-core-android:$korgeVersion"
    implementation "com.soywiz.korge:korge-foundation-android:$korgeVersion"
}
